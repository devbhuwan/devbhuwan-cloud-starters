buildscript {

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.16"
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.2.RELEASE"
}

apply from: "$rootDir/gradle/dependencies.gradle"

allprojects {
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        mavenRepoUrl.split(',').each { repoUrl -> maven { url repoUrl } }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    compileJava {
        options.encoding = 'UTF-8'
    }

    archivesBaseName = "devbhuwan-${it.name}"

    sourceSets {
        test {
            java.srcDirs = ['src/test/java', 'src/test/groovy']
        }
    }

    dependencies {
        compile("$appLibs.lombok")
        compile("$appLibs.commonLang3")

        testCompile("$appLibs.junit")
        testCompile("$appLibs.assertj")
        compile("$appLibs.groovyAll")
        testCompile("$appLibs.spockCore")
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.SR1"
            mavenBom "org.springframework.boot:spring-boot-starter-parent:1.5.4.RELEASE"
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) { from components.java }
        }
    }

    jar {
        into("META-INF/maven/$project.group/$project.name") {
            from { generatePomFileForMavenJavaPublication }
            rename ".*", "pom.xml"
        }
    }

}